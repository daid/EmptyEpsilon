set(CMAKE_SYSTEM_NAME Windows)

set(CMAKE_C_COMPILER x86_64-w64-mingw32-gcc-posix)
set(CMAKE_CXX_COMPILER x86_64-w64-mingw32-g++-posix)
set(CMAKE_RC_COMPILER x86_64-w64-mingw32-windres)

set(CMAKE_CXX_FLAGS_RELEASE_INIT "-flto")
set(CMAKE_CXX_FLAGS_RELWITHDEBINFO_INIT "-flto")
set(CMAKE_C_FLAGS_RELEASE_INIT "-flto")
set(CMAKE_C_FLAGS_RELWITHDEBINFO_INIT "-flto")
set(CMAKE_EXE_LINKER_FLAGS_RELEASE_INIT "-flto")
set(CMAKE_EXE_LINKER_FLAGS_RELWITHDEBINFO_INIT "-flto")

set(CMAKE_FIND_ROOT_PATH_MODE_PROGRAM NEVER)
set(CMAKE_FIND_ROOT_PATH_MODE_LIBRARY ONLY)
set(CMAKE_FIND_ROOT_PATH_MODE_INCLUDE ONLY)
set(CMAKE_FIND_ROOT_PATH_MODE_PACKAGE ONLY)

set(MING_DLL_PATH /usr/lib/gcc/x86_64-w64-mingw32/4.8/)

function(execute_process_ex)
    execute_process(${ARGV} RESULT_VARIABLE RES)
    if(RES)
        message(FATAL_ERROR "Failed to execute: ${ARGV}")
    endif()
endfunction()

if(NOT SDL2_ROOT)
    set(WIN64_BASE "${CMAKE_CURRENT_LIST_DIR}/_win32/")
    set(CMAKE_SYSROOT "${WIN64_BASE}")

    if(NOT IS_DIRECTORY "${WIN64_BASE}/SDL2-2.32.8")
        file(DOWNLOAD https://github.com/libsdl-org/SDL/releases/download/release-2.32.8/SDL2-devel-2.32.8-mingw.zip "${WIN64_BASE}/SDL2.32.8.tar.gz" SHOW_PROGRESS EXPECTED_HASH SHA256=2f0a74c2eb3f7ffb26aeefce733ce75f5a57881adf3fab92b2430805ff7249e2)
        execute_process_ex(COMMAND ${CMAKE_COMMAND} -E tar xzvf SDL2.32.8.tar.gz WORKING_DIRECTORY "${WIN64_BASE}")
        execute_process_ex(COMMAND sed -i "s|set(prefix .*)|set(prefix \"${WIN64_BASE}/SDL2-2.32.8/x86_64-w64-mingw32/\")|g" ${WIN64_BASE}/SDL2-2.32.8/x86_64-w64-mingw32/lib/cmake/SDL2/sdl2-config.cmake)
    endif()
    
    set(SDL2_ROOT "${WIN64_BASE}/SDL2-2.32.8/x86_64-w64-mingw32/")

    option(ENABLE_CRASH_LOGGER "" ON)
    set(DRMINGW_ROOT "${WIN64_BASE}/drmingw-0.8.2-win64")
    if(NOT IS_DIRECTORY "${DRMINGW_ROOT}")
        file(DOWNLOAD https://github.com/jrfonseca/drmingw/releases/download/0.8.2/drmingw-0.8.2-win64.7z ${WIN64_BASE}/drmingw.7z SHOW_PROGRESS)
        execute_process_ex(
            COMMAND ${CMAKE_COMMAND} -E tar "x" "drmingw.7z"
            WORKING_DIRECTORY ${WIN64_BASE})
    endif()
endif()
